buildscript {
  repositories {
    mavenCentral()
  }
}

// gradle plugins
plugins {
  id 'application'
  // auto format source files
  id 'com.diffplug.spotless' version '8.1.0'
  // analyze code and find potential bugs
  id 'pmd'
  id 'com.github.spotbugs' version '6.4.8'
  // test coverage report
  id 'jacoco'
  // maven plugin to upload to repository
  id 'maven-publish'
  // some lints for dependencies
  id 'nebula.lint' version '21.1.3'
  // resolve git branch name
  id 'org.ajoberstar.grgit' version '5.3.3'

  id "com.github.bjornvester.wsdl2java" version "2.0.2"
}

group = 'de.ebsnet'
// version must be set _before_ applying helper.gradle
version = '1.4.0'

dependencyLocking {
  lockAllConfigurations()
}

spotless {
  java {
    forbidWildcardImports()
    googleJavaFormat()
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
  options.compilerArgs = [
    '-Xlint',
    '-g'
  ]
  // make build tasks depend on the formatter task
  // automatically format the code when building
  dependsOn 'spotlessApply'
}

tasks.withType(Javadoc).configureEach {
  options.encoding = 'UTF-8'
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

// dependency lints
gradleLint {
  // run all lints and print warnings
  rules = ['all-dependency']
  // fail the build if unused dependencies are defined
  criticalRules = ['unused-dependency']

  // do not run linter when the build fails
  // TODO: not compatible with Gradle > 7.6
  // gradleLint.autoLintAfterFailure = false

  // do not run linter on task publish
  skipForTask('publish')
  // skip linter on IntelliJ init task
  skipForTask('prepareKotlinBuildScriptModel')
  skipForTask('ijDownloadSources')
  // dependabot breaks sometimes...
  skipForTask(':ForceDependencyResolutionPlugin_resolveAllDependencies')
}

// remote repositories for 3rd party dependencies
repositories {
  mavenCentral()
}

import com.github.spotbugs.snom.SpotBugsTask
tasks.withType(SpotBugsTask).configureEach {
  reports {
    html.required = true
    xml.required = true
  }
}

spotbugs {
  excludeFilter = file('config/spotbugs/exclude.xml')
}

tasks.withType(Pmd).configureEach {
  reports {
    html.required = true
  }
}

pmd {
  ruleSetFiles = files('config/pmd/pmd-all-java.xml')
  // this is needed for exclude patterns to work
  // this resets the default rulesets that are set by the gradle plugin itself
  ruleSets = []
}

def versions = [
  bc: '1.83',
  junit: '6.0.2',
]

dependencies {
  implementation "org.bouncycastle:bcpkix-jdk18on:${versions.bc}"
  implementation "org.bouncycastle:bcprov-jdk18on:${versions.bc}"
  implementation "org.bouncycastle:bctls-jdk18on:${versions.bc}"
  implementation "org.bouncycastle:bcutil-jdk18on:${versions.bc}"

  implementation 'info.picocli:picocli:4.7.7'

  implementation "com.sun.xml.ws:jaxws-ri:4.0.3"
  implementation "com.sun.xml.ws:jaxws-rt:4.0.3"
  implementation "jakarta.xml.bind:jakarta.xml.bind-api:4.0.4"
  implementation "jakarta.xml.soap:jakarta.xml.soap-api:3.0.2"
  implementation "jakarta.xml.ws:jakarta.xml.ws-api:4.0.2"

  testImplementation(platform("org.junit:junit-bom:${versions.junit}"))
  testImplementation "org.junit.jupiter:junit-jupiter-api"

  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher"

  // additional spotbugs detectors
  spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.14.0'
  // TODO activate and fix warnings
  // spotbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.4.7'// EXPERIMENTAL
  // provides annotations to suppress spotbugs warnings
  compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"

  constraints {
    wsdl2java("org.apache.cxf:cxf-core:4.1.4") {
      because("CVE-2024-28752")
    }
  }
}

test {
  useJUnitPlatform()
}

def latestCommitHash = grgit.head().abbreviatedId

// MANIFEST.mf properties
jar {
  manifest {
    attributes 'Name': project.name
    attributes 'Implementation-Version': latestCommitHash
    attributes 'Implementation-Vendor': 'EBSnet GmbH'
    attributes 'Specification-Version': archiveVersion
    attributes 'Specification-Vendor': 'EBSnet GmbH'
  }
}

test {
  finalizedBy jacocoTestReport
}

// write application version to version property file
import org.apache.tools.ant.filters.*
processResources {
  filter ReplaceTokens, tokens: [
    "version": project.property("version"),
    "commit": latestCommitHash
  ]
}

application {
  // Define the main class for the application.
  mainClass = 'de.ebsnet.crmf.CSRGenerator'
}

wsdl2java {
  // cxfVersion = "${versions.ws}"
  // includes = [
  //   "src/main/resources/wsdl/TerminalAuthentication/CVCA/TerminalAuthentication-CVCA-TowardsDV.wsdl",
  // ]
}

spotlessJava {
  dependsOn ':wsdl2java'
}

// vim: set filetype=groovy ts=2 sw=2 tw=120 et :
